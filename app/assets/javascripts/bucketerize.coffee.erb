class @ModalBucketerizeHook
  constructor: (@api) ->
    @append_modal()
    @$modal_buckets = jQuery('#modal-buckets')
    @resource_ids = [@api.resource_id]
    @_init()

  _init: () ->
    @bucket_ids = []
    @api.$el.on 'click', =>
      @$modal_buckets.modal('show')

    # 创建按钮点击
    @$modal_buckets.find('.new').click () =>
      @modal_new_bucket.modal('show')

    @modal_new_bucket = jQuery('#modal-new-bucket')

    @modal_new_bucket.find('.create').click () =>
      name = @modal_new_bucket.find('.name').val()
      desc = @modal_new_bucket.find('.desc').val()
      @api.create_bucket(name, desc)

    # 点击事件绑定：添加、移除到bucket
    that = this
    @$modal_buckets.on 'click', '.buckets a.list-group-item', ->
      if $(this).hasClass('active')
        bucket_id = $(this).data('id')
        that.bucket_ids = [bucket_id]
        that.api.remove_from()
      else
        bucket_id = $(this).data('id')
        that.bucket_ids = [bucket_id]
        that.api.add_to()

  el_click: (el) ->
    @api.get_all_buckets()
    @$modal_buckets.modal('show')

  get_resources_buckets_success: (data) =>
    str = ''
    that = this
    for resource in data
      added = false
      jQuery.each resource.buckets, ->
        bucket = this
        added = added || bucket.added
        str += that._str_bucket(bucket.id, bucket.name, bucket.added)
      that.api.$el.select("[data-bucketerize-resource-id=#{resource.id}]").addClass('active') if added
    @$modal_buckets.find('.buckets').html(str)

  create_bucket_success: (bucket) =>
    that = this
    str = @_str_bucket(bucket.id, bucket.name, false)
    @$modal_buckets.find('.buckets').append str

    @modal_new_bucket.find('.name').val('')
    @modal_new_bucket.find('.desc').val('')
    @modal_new_bucket.modal('hide')

  add_to_success: (resource_ids, buckets) =>
    that = this
    jQuery.each buckets, ->
      bucket = this
      that.$modal_buckets.find("[data-id=#{bucket.id}]").addClass('active')
      that.api.$el.addClass('active')

    jQuery.each resource_ids, ->
      resource_id = this
      that.api.$el.select("[data-resource-id=#{resource_id}]").addClass('active')

  remove_from_success:  (resource_ids, buckets) =>
    that = this
    jQuery.each buckets, ->
      bucket = this
      that.$modal_buckets.find("[data-id=#{bucket.id}]").removeClass('active')
    
    @api.$el.removeClass('active') if @$modal_buckets.find('.list-group-item.active').length == 0

  replace_buckets_success: (resource_ids, buckets) =>

    that = this
    jQuery.each buckets, ->
      bucket = this
      that.$modal_buckets.find("[data-id=#{bucket.id}]").removeClass('active')


  assigned_resource_ids: () ->
    @resource_ids

  assigned_bucket_ids: ->
    @bucket_ids

  _str_bucket: (id, name, added) ->
    "<a class=\"list-group-item #{if added then "active" else ""}\" data-id=\"#{id}\" id=\"bucket_#{id}\"><strong>#{name}</strong><span class=\"bucket-meta\">更新时间</span></a>"

  append_modal: ->
    jQuery('body').append @modal_template()
    
  modal_template: ->
    """
            <div class='modal fade' id='modal-buckets'>
              <div class='modal-dialog'>
                <div class='modal-content'>
                  <div class='modal-header'>
                    <button aria-label='Close' class='close' data-dismiss='modal' type='button'>
                      <span aria-hidden='true'>&times;</span>
                    </button>
                    <h4 class='modal-title'>添加 #{@api.resource_type} 至 #{@api.bucket_type}</h4>
                  </div>
                  <div class='modal-body'>
                    <div class='list-group buckets'></div>
                    <ol class='group'></ol>
                  </div>
                  <div class='modal-footer'>
                    <button class='btn btn-default' data-dismiss='modal' type='button'>Done</button>
                    <a class='new btn btn-primary' href='javascript:;'>+ 新建#{@api.bucket_type}</a>
                  </div>
                </div>
              </div>
            </div>
            <div class='modal fade' id='modal-new-bucket'>
              <div class='modal-dialog'>
                <div class='modal-content'>
                  <div class='modal-header'>
                    <button aria-label='Close' class='close' data-dismiss='modal' type='button'>
                      <span aria-hidden='true'>&times;</span>
                    </button>
                    <h4 class='modal-title'>新建#{@api.bucket_type}</h4>
                  </div>
                  <div class='modal-body'>
                    <div class='form-group'>
                      <input class='form-control name' placeholder='名称' type='text'>
                    </div>
                    <div class='form-group'>
                      <input class='form-control desc' placeholder='描述' type='text'>
                    </div>
                  </div>
                  <div class='modal-footer'>
                    <button class='btn btn-default' data-dismiss='modal' type='button'>取消</button>
                    <a class='create btn btn-primary' href='javascript:;'>提交</a>
                  </div>
                </div>
              </div>
            </div>
      """

class @StandardBucketerizeHook
  constructor: (@api) ->
    @resource_ids = [@api.resource_id]
    @_init()

  _init: () ->
    @bucket_ids = []
    that = this
    @api.$el.on 'click', ->
      $this = jQuery(this)
      if $this.hasClass('active')
        that.api.remove_from()
      else
        that.api.add_to()

  get_resources_buckets_success: (data) =>
    that = this
    jQuery.each data, (index) ->
      resource_id = this['id']
      jQuery.each this['buckets'], (index1) ->
        if this['added'] and this['name'] == '默认'
          jQuery.each that.api.$el, ->
            $this = jQuery(this)
            $this.addClass('active') if $this.data('bucketerizeResourceId') == resource_id and !$this.hasClass('active')
        else
          jQuery.each that.api.$el, ->
            $this = jQuery(this)
            $this.removeClass('active') if $this.data('bucketerizeResourceId') == resource_id

  remove_from_success:  (resource_ids, buckets) =>
    resource_id = resource_ids[0]
    @api.$el.each (index)->
      $this = jQuery(this)
      $this.removeClass('active') if $this.data('bucketerizeResourceId') == resource_id

  add_to_success: (resource_ids, buckets) =>
    resource_id = resource_ids[0]
    @api.$el.each (index)->
      $this = jQuery(this)
      $this.addClass('active') if $this.data('bucketerizeResourceId') == resource_id and !$this.hasClass('active')

  assigned_resource_ids: () ->
    @resource_ids

  assigned_bucket_ids: ->
    @bucket_ids

  error: (error) ->
    console.log 'error'
    console.log error

class @BucketerizeHook
  constructor: (@api) ->
    console.log 'bucketerize hook constructor'

  el_click: (el) ->
    console.log 'bucketerize hook el click'

  get_all_buckets_success: (buckets) =>
    console.log 'bucketerize hook get all buckets success'

  get_resources_buckets_success: (data) =>
    console.log 'bucketerize hook get resources buckets  success'

  create_bucket_success: (bucket) =>
    console.log 'bucketerize hook create bucket success'

  add_to_success: (resource_ids, buckets) =>
    console.log 'bucketerize hook add to success'

  replace_buckets_success: (resource_ids, buckets) =>
    console.log 'bucketerize hook replace buckets success'

  remove_from_success: (bucket) ->
    console.log 'bucketerize hook remove_from_success'

  error: (error) ->
    console.log 'error'
    console.log error

  assigned_resource_ids: ->
    console.log 'assigned_resource_ids'
    resource_ids = []

  assigned_bucket_ids: ->
    console.log 'assigned_bucket_ids'
    bucket_ids = []

class @Bucketerize
  constructor: (@configs)->
    @_init()

  _init: ->
    for key, val of @_default_configs
      @configs[key] ||= val

    @selector = @configs['selector']
    @path_fix = @configs['path_fix']

    if @selector
      @$el = jQuery @selector
      if @$el.length > 0
        jQuery.each @$el, (index) ->
          $this = jQuery(this)
          $this.data 'el', $this
          configs = $this.data()

          api = new BucketerizeApi(configs)

  _default_configs:
    path_fix: "<%= Bucketerize.get_mount_prefix %>"
    selector: '[data-rel=bucketerize]'

class @BucketerizeApi
  constructor: (@configs)->
    @_init()

  _default_configs:
    path_fix: "<%= Bucketerize.get_mount_prefix %>"
    bucketerizeBucketType: "Bucket"

  _init: ->
    for key, val of @_default_configs
      @configs[key] ||= val

    @path_fix = @configs["path_fix"]
    @buckets_path = @path_fix + "/buckets"
    @bucketings_path = @path_fix + "/bucketings"

    @$el = @configs['el']
    return if @$el.length == 0
    if @$el.length > 0
      @resource_id = @configs['bucketerizeResourceId']
      @resource_type = @configs['bucketerizeResourceType']
      @bucket_type = @configs["bucketerizeBucketType"]

      if @configs['bucketerizeMode'] == 'multi'
        @configs['hook_class'] = ModalBucketerizeHook if !@configs['hook_class']
      else if @configs['bucketerizeMode'] == 'standard'
        @configs['hook_class'] = StandardBucketerizeHook if !@configs['hook_class']
      else
        @configs['hook_class'] = BucketerizeHook if !@configs['hook_class']

      @hook = new @configs["hook_class"](@)

      @get_resources_buckets()

  get_all_buckets: () ->
    if @$el.length > 0
      resource_ids = @hook.assigned_resource_ids()
      if !@bucket_type or !@resource_type
        return @hook.error {error: "params blank"}

    @_get_buckets()

  get_resources_buckets: () ->
    if @$el.length > 0
      if !@bucket_type or !@resource_type
        return @hook.error {error: "params blank"}

      @_get_buckets(@hook.assigned_resource_ids())

  _get_buckets: (resource_ids) ->
    jQuery.ajax
      url: @buckets_path
      method: "GET"
      data:
        bucket_type: @bucket_type
        resource_type: @resource_type
        "resource_ids[]": resource_ids
      success: (res) =>
        if res['error']
          @hook.error(res.error)
        else
          if res['action'] == 'get_buckets'
            @hook.get_all_buckets_success(res.result)
          else if res['action'] == 'get_resources_buckets'
            @hook.get_resources_buckets_success(res.result)

  create_bucket: (bucket_name, bucket_desc) ->
    if @$el.length > 0
      @_create_bucket(@bucket_type, bucket_name, bucket_desc)

  add_to: () ->
    if @$el.length > 0
      @_add_to_bucket(@resource_type, @hook.assigned_resource_ids(), @bucket_type, @hook.assigned_bucket_ids())

  remove_from: () ->
    if @$el.length > 0
      @_remove_from_bucket(@resource_type, @hook.assigned_resource_ids(), @bucket_type, @hook.assigned_bucket_ids())

  replace_buckets: () ->
    if @$el.length > 0
      @_replace_buckets(@resource_type, @hook.assigned_resource_ids(), @bucket_type, @hook.assigned_bucket_ids())

  _create_bucket: (bucket_type, name, desc) ->
    jQuery.ajax
      url: @buckets_path
      method: "POST"
      data: 
        bucket_type: bucket_type
        name: name
        desc: desc
      success: (res) =>
        if res['error']
          @hook.error(res.error)
        else
          @hook.create_bucket_success(res.result)

  _add_to_bucket: (resource_type, resource_ids, bucket_type, bucket_ids) ->
    jQuery.ajax
      url: @bucketings_path
      method: "POST"
      data:
        resource_type: resource_type
        "resource_ids[]": resource_ids
        bucket_type: bucket_type
        "bucket_ids[]": bucket_ids
      success: (res) =>
        if res['error']
          @hook.error(res.error)
        else
          @hook.add_to_success(res.result.resource_ids, res.result.buckets)

  _remove_from_bucket: (resource_type, resource_ids, bucket_type, bucket_ids) ->
    jQuery.ajax
      url: @bucketings_path
      method: "DELETE"
      data:
        resource_type: resource_type
        "resource_ids[]": resource_ids
        bucket_type: bucket_type
        "bucket_ids[]": bucket_ids
      success: (res) =>
        if res['error']
          @hook.error(res.error)
        else
          @hook.remove_from_success(res.result.resource_ids, res.result.buckets)


  _replace_buckets: (resource_type, resource_ids, bucket_type, bucket_ids) ->
    jQuery.ajax
      url: @bucketings_path
      method: "GET"
      data:
        resource_type: resource_type
        "resource_ids[]": resource_ids
        bucket_type: bucket_type
        "bucket_ids[]": bucket_ids
      success: (res) =>
        if res['error']
          @hook.error(res.error)
        else
          @hook.replace_buckets_success(res.result.resource_ids, res.result.buckets)

